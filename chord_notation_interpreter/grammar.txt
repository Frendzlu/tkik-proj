start: measure_krnl

measure_krnl: measure_krnl measure_krnl
            | measure_krnl? preamble LPAR measure_krnl RPAR measure_suffix
            | PIPE subdiv? time_sgn? chords PIPE measure_suffix

subdiv: p2_number
time_sgn: LT number COLON p2_number GT
preamble: AT number SEMI
measure_suffix: (jmp_sgn | group_repeat)*

jmp_sgn: AMP number SEMI

group_repeat: PERCENT number?

chords: multi_chord time_extension? chord_repeat_opt? chords?
      | time_extension? multi_chord jmp_sgn chords?

chord_repeat_opt: PERCENT
                | PERCENT chord_repeat_opt
                |

multi_chord: chord
           | chord DASH chord
           | chord SLASH root_note
           | NC
           | UNDERSCORE

time_extension: STAR number

chord: root_note mode? modifiers*

root_note: base_note root_note_modifier?

base_note: NOTES

root_note_modifier: FLAT | HASH

mode: MODE

modifiers: chord_base_size? component_modifiers? additions? suspensions? reductions?

chord_base_size: number  // restricted to: 7, 9, 11, 13 (enforce in code)

component_modifiers: (modifier? component_number)+

modifier: FLAT | HASH

component_number: number  // restricted to: 5, 6, 7, 9, 11

additions: ADD modifier? number+

suspensions: SUS ("2" | "4")

reductions: NO ("3" | "5")

p2_number: number     // restrict values to powers of 2 (1â€“256) in code

number: DIGIT_NONZERO DIGIT*

DIGIT_NONZERO: /[1-9]/
DIGIT: /[0-9]/
SLASH: "/"
DASH: "-"
STAR: "*"
AMP: "&"
PERCENT: "%"
HASH: "#"
FLAT: "b"
LT: "<"
GT: ">"
LPAR: "("
RPAR: ")"
PIPE: "|"
SEMI: ";"
AT: "@"
COLON: ":"
UNDERSCORE: "_"
NC: "N.C."
MODE: "maj" | "m" | "dim" | "aug"
ADD: "add"
NO: "no"
SUS: "sus"
NOTES: "A" | "B" | "C" | "D" | "E" | "F" | "G"


%import common.WS
%ignore WS
